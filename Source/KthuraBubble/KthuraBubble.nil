// License Information:
// KthuraBubble/KthuraBubble.nil
// Kthura NIL Glue
// version: 19.11.23
// Copyright (C)  Jeroen P. Broks
// This software is provided 'as-is', without any express or implied
// warranty.  In no event will the authors be held liable for any damages
// arising from the use of this software.
// Permission is granted to anyone to use this software for any purpose,
// including commercial applications, and to alter it and redistribute it
// freely, subject to the following restrictions:
// 1. The origin of this software must not be misrepresented; you must not
// claim that you wrote the original software. If you use this software
// in a product, an acknowledgment in the product documentation would be
// appreciated but is not required.
// 2. Altered source versions must be plainly marked as such, and must not be
// misrepresented as being the original software.
// 3. This notice may not be removed or altered from any source distribution.
// End License Information



#say "Kthura!"

table _actorlist


class KthuraActor
	
	readonly string Tag
	
	int ParentID
		
	void CONSTRUCTOR(string sTag,parent)
		assert(parent and type(parent)=="table" and parent[".classname"]=="Kthura","None or incorrect parent given") //"\n"..type(parent).." / "..(parent or {[".classname"]="<nil>"})[".classname"])
		self.Tag=sTag
		self.ParentID = parent.ID
		_actorlist[self.Tag] = self
	end
	
	void DESTRUCTOR()
		print("Kthura Actor destroyed!")
		#pure
		if CSay then CSay("Kthura Actor Destroyed!") end
		#endpure
	end
	
	get bool Exists
		return BKTHURA:ActorExists(self.ParentID,self.Tag)
	end
	
	get int X
		return BKTHURA:GetX(self.ParentID,self.Tag)
	end
	
	get int Y
		return BKTHURA:GetY(self.ParentID,self.Tag)
	end
	
	set int X
		BKTHURA:SetX(self.ParentID,self.Tag,value)
	end
	
	set int Y
		BKTHURA:SetY(self.ParentID,self.Tag,value)
	end
	
	get var Coords
		return self.X,self.Y
	end
	
	get string Texture
		return BKTHURA:GetObjTex(self.ParentID,self.Tag)
	end
	
	set string Texture
		BKTHURA:SetObjTex(self.ParentID,self.Tag,value)
	end
	
	get string Wind
		return BKTHURA:GetActorWind(self.ParentID,self.Tag)
	end
	
	set string Wind
		BKTHURA:SetActorWind(self.ParentID,self.Tag,value)
	end
	
	void Spawn(string exittag,onlyifnew)
		if onlyifnew and self.Exists
			return 
		end
		BKTHURA:Spawn(self.ParentID,self.Tag,exittag)
	end
	
	void SpawnCoords(int x, int y, onlyifnew)
		if onlyifnew and self.Exists
			return 
		end
		BKTHURA:SpawnCoords(self.ParentID,self.Tag,x,y)
	end
	
	void CoordSpawn(int x, int y, onlyifnew)
		$SpawnCoords(x,y,onlyifnew)
	end
	
	void Destroy()
		//self.parentmap = nil;
	end
	
	#say "WALKTO!"
	bool WalkTo(A,B,C)
		if (type(A)=="number" and type(B)=="number")
			#pure
			if (CSay) then CSay("Walkto request: ("..A..","..B.."); Real: "..tostring(C~=false)) end
			#endpure
			BKTHURA:WalkToCoords(self.ParentID,self.Tag,A,B,C!=false)
		elseif type(A)=="string"
			#pure
			if (CSay) then CSay("Walkto request: Spot:'"..A.."'; Real: "..tostring(C~=false)) end
			#endpure
			BKTHURA:WalkToSpot(self.ParentID,self.Tag,A)
		else
			return false
		end		
		#pure
		if (CSay) then CSay("Success received from Kthura is: "..tostring(BKTHURA.WalkSuccess)) end
		#endpure
		return BKTHURA.WalkSuccess
	end
	#say "/WALKTO"
	
	get bool Walking
		return BKTHURA:Walking(self.ParentID,self.Tag)
	end
	
	set bool Walking
		BKTHURA:SetWalking(self.ParentID,self.Tag,value)
	end
	
	bool IsInZone(string zone)
		return BKTHURA:ObjectInZone(self.ParentID,self.Tag,zone)
	end
	
	bool Moving(self.ParentID,self.Tag)

	set bool Moving
		BKTHURA:SetMoving(self.ParentID,self.Tag,value)
	end
	
	
	bool MoveTo(A,B,C)
		if self.Walking
			return false
		end
		if (type(A)=="number" and type(B)=="number")
			#pure
			if (CSay) then CSay("Walkto request: ("..A..","..B.."); Real: "..tostring(C~=false)) end
			#endpure
			BKTHURA:MoveToCoords(self.ParentID,self.Tag,A,B) //,C!=false)
		elseif type(A)=="string"
			#pure
			if (CSay) then CSay("Walkto request: Spot:'"..A.."'; Real: "..tostring(C~=false)) end
			#endpure
			BKTHURA:MoveToSpot(self.ParentID,self.Tag,A)
		else
			return false
		end		
		#pure
		if (CSay) then CSay("Success received from Kthura is: "..tostring(BKTHURA.WalkSuccess)) end
		#endpure
		return true
	end
	
end


class Kthura
	readonly int ID
	static delegate PermaStore
	
	readonly table Actor
	
	string Obj
	table ObjInt
	table ObjString
	table ObjData
	
	get int ScrollX
		return BKTHURA:GetScroll(self.ID,"X")
	end
	
	set int ScrollX
		BKTHURA:SetScroll(self.ID,"X",value)
	end

	get int ScrollY
		return BKTHURA:GetScroll(self.ID,"Y")
	end
	
	set int ScrollY
		BKTHURA:SetScroll(self.ID,"Y",value)
	end
	
	get string Layer
		return BKTHURA:GetLayer(self.ID)
	end
	
	set string Layer
		// CSay("Switching to layer: "..value)
		BKTHURA:SetLayer(self.ID,value)
	end
	
	get table Layers
		string glay 
		glay = BKTHURA:GetLayers(self.ID)
		#pure
		return assert( (loadstring or load)(glay)() )
		#endpure
	end
	
	get var EachLayer
		var r
		int i
		r = $Layers
		return var ()
			i++
			if i>#r 
				return nil
			else
				return r[i]
			end
		end
	end
	
	bool Blocked(int x, int y, real)
		return BKTHURA:Blocked(self.ID,x,y,real==true)
	end
	
	bool InObj(string tag,int x, int y)
		return BKTHURA:InObj(self.ID,tag,x,y)
	end
	
	void VisibilityByZone(string zone,bool value)
		BKTHURA:VisibleByZone(self.ID,zone,value)
	end
	
	void HideByZone(string zone)
		self.VisibilityByZone(zone,false)
	end
	
	void ShowByZone(string zone)
		self.VisibilityByZone(zone,true)
	end
	
	void RemapDominance()
		BKTHURA:DomRemap(self.ID)
	end
	
	void HideByLabel(string label)
		BKTHURA:HideByLabel($ID,label)
	end
	
	void ShowByLabel(string label)
		BKTHURA:ShowByLabel($ID,label)
	end
	
	table Tags(alay)
		string lay
		string gtags
		lay = alay or $Layer
		gtags = BKTHURA:GetTags(self.ID,lay)
		#pure
		return assert( (loadstring or load)(gtags)() )
		#endpure
	end
	
	var EachTag(alay)
		var r
		int i
		r = $Tags(alay)
		return var ()
			i++
			if i>#r 
				return nil
			else
				return r[i]
			end
		end
	end
	
	bool HasTag(string A,B)
		if B
			return BKTHURA:HasTag(self.ID,A,tostring(B))
		else
			return BKTHURA:LHasTag(self.ID,A)
		end
	end
		
	get bool AutoRemap
		return BKTHURA:GetAutoRemap(self.ID)
	end
	
	set bool AutoRemap
		BKTHURA:SetAutoRemap(self.ID,value)
	end
	
	void Remap()
		BKTHURA:TotalRemap(self.ID)
	end
	
	void Kill(string tag)
		BKTHURA:Kill(self.ID,tag)
	end
	
	void PermaKill(string tag)
		assert(self.PermaStore,"Kthura.PermaStore not configured! Cannot PermaKill!")
		Kill(tag)
		self.PermaStore('LocalKthura.Kill("'..tag..'"))\n')
	end
	
	void Draw(x,y)
		BKTHURA:Draw(self.ID,x or 0, y or 0)
	end
	
	table CountObjects()
		string tstring
		tstring = BKTHURA:CountObjects(self.ID)
		//CSay(tstring)
		#pure
		return (loadstring or load)(tstring)()
		#endpure
	end
	
	void Destroy()
		if self.ID!=0
			BKTHURA:Destroy(self.ID)
		end
		table kill
		for k,v in pairs(_actorlist[self.ID])
			// v.ParentMap=nil;
			kill[#kill+1]=k
		end
		for _,k in ipairs(kill)
			_actorlist[self.ID][k].Destroy()
			_actorlist[self.ID][k]=nil;
		end
		_actorlist[self.ID]=nil;
	end
	
	void DESTRUCTOR()
		self.Destroy()
	end
	
	private table mdata
	set table Data
		error("The field \"Data\" is read-only!")
	end
	
	get table Data
		return self.mdata
	end
	
	bool ObjInZone(string ObjTag,string zone)
		return BKTHURA:ObjectInZone(self.ID,ObjTag,zone)
	end
	
	int ObjX(string tag)
		return BKTHURA:GetX(self.ID,tag)
	end
	
	int ObjY(string tag)
		return BKTHURA:GetY(self.ID,tag)
	end
	
	void HideObject(string tag)
		BKTHURA:HideObject($ID,tag)
	end
	
	void ShowObject(string tag)
		BKTHURA:ShowObject($ID,tag)
	end
	
	void CreateTiledArea(string Texture,int x, int y, int w, int h, tag)
		BKTHURA:CreateTiledArea($ID,Texture,x,y,w,h,tag or "")
	end
	
	void CONSTRUCTOR(mapname)
		switch type(mapname)
			case "nil"
				self.ID = BKTHURA:Create()
			case "string"
				self.ID = BKTHURA:Load(mapname)
			default
				error("Invalid input for initiating a Kthura Map! ("+type(mapname)+")")
		end
		setmetatable(self.mdata,{
			['__newindex'] = void(t,string k,string v)
				BKTHURA:SetData(self.ID,k,v)
			end,
			['__index'] = string(t,k)
				return BKTHURA:GetData(self.ID,k)
			end,
			['__call'] = table(t)
				string tempscript
				table temptable
				tempscript = BKTHURA:GetDataKeys(self.ID)
				// CSay(tempscript) // debug
				temptable = assert(NIL.LuaLoadString(tempscript))
				return temptable()
			end
		})
		_actorlist[self.ID] = {}
		table ActorMeta		
		ActorMeta.__newindex = void()
			error("Kthura.Actor is a read-only table!")
		end		
		ActorMeta.__index = var(t,string k)
			_actorlist[self.ID][k] = _actorlist[self.ID][k] or new KthuraActor(k,self)
			return _actorlist[k]
		end
		setmetatable(self.Actor,ActorMeta)
		setmetatable(self.ObjInt,{
			['__newindex'] = void(t,string k,int v)
				//error("ObjInt is in the current version of Kthura for Bubble read-only!");
				BKTHURA:SetObjInt(self.ID, self.Layer,self.Obj,k,v)
			end,
			['__index'] = number(t,string k)
				return BKTHURA:ObjInt(self.ID, self.Layer,self.Obj,k)
			end,
			['__gc'] = void()
				// Silly Easter Egg! :P
				CSay("Did you know that Darth Vader never said \"Luke! I am your father!\"?")
			end
		})
		setmetatable(self.ObjString,{
			['__newindex'] = void(t,string k,string v)
				//error("ObjString is in the current version of Kthura for Bubble read-only!");
				BKTHURA:SetObjString(self.ID, self.Layer,self.Obj,k,v)
			end,
			['__index'] = number(t,string k)
				return BKTHURA:ObjString(self.ID, self.Layer,self.Obj,k)
			end
		})
		setmetatable(self.ObjData,{
			['__newindex'] = void(t,string k,string v)
				//error("ObjString is in the current version of Kthura for Bubble read-only!");
				BKTHURA:SetObjData(self.ID, self.Layer,self.Obj,k,v)
			end,
			['__index'] = number(t,string k)
				return BKTHURA:ObjData(self.ID, self.Layer,self.Obj,k)
			end
		})
		

	end
	
	
end











